RPL

::
  CLOSEUART
  RECLAIMDISP
  TURNMENUOFF
  ABUFF

  #E
  #1
  MAKEGROB


  DROP
CODEM
SAVE
C=DAT1 A
D1=C
D1=D1+ 5  %SALTO PROLOGO
D1=D1+ 5  %SALTO SIZE
D1=D1+ 5  %SALTO NUMERO DE FILAS
D1=D1+ 5  %SALTO NUMERO DE COLUMNAS
C=D1 A
R1=C A
R2=C A
D1=D1+ 2  %SALTA PRIMERAS 3 COLUMNA
C=D1 A
R0=C A
%**********DIBUJO DE EJE DE COORDENADAS********
LA 64     %CONTADOR DE 64 LINEAS
*EJE
C=R1 A
D1=C
LC 01
DAT1=C B   %imprime el dato en pantalla
C=D1 A
C=C+34 A
R1=C A
A=A-1 B
?A=0 B
GOYES COORDENADAS
GOTO EJE
*COORDENADAS
A=0 W
LA 5
*COORD2
C=R2 A
D1=C
LC 07
DAT1=C B   %imprime el dato en pantalla
C=D1 A
C=C+68 A
C=C+68 A
C=C+68 A
C=C+68 A
C=C+68 A
C=C+68 A
C=C+34 A
R2=C A
A=A-1 B
?A=0 B
GOYES SIGUE
GOTO COORD2
*SIGUE
C=R0 A
D1=C
*PRINCIPAL
%*****************TRANSMISION******************
%CIERRA PUERTO PARA BORRAR BUFFER
   $1B01100 %HACE D1= 00110 UART INTERRUPT REGISTER
   C=DAT0 B
   CBIT=0 3 %DESHABILITA EL RECV BUFFER DEL WIRE ENABLE SERIAL
   DAT0=C B   %MUEVE EL VALOR A LA DIRECCION
%COMIEZA A RECOGER DATO
   $1BD0100 %HACE D1= 0010D QUE ES EL ENTER POINT DE
   %VELOCIDAD
   C=DAT0 B   %CARGA EL CONTENIDO DE LA DIRECION DEL SETEO
   CBIT=0 0   %SETEA A 1 EL BIT 0
   CBIT=1 1   %SETEA A 1 EL BIT 1
   CBIT=1 2   %SETEA A 1 EL BIT 2
   DAT0=C B   %MUEVE EL VALOR SETEADO A LA DIRECCION DEL EP
   $1B01100 %HACE D1= 00110 UART INTERRUPT REGISTER
   C=DAT0 B
   CBIT=1 3 %HABILITA EL RECV BUFFER DEL WIRE ENABLE SERIAL
   DAT0=C B   %MUEVE EL VALOR A LA DIRECCION
   *BUCLE     %SE FIJA SI EL BIT 0 DE LA DIRECCION
   GOSUBL ON
   $1B11100   %SE FIJA EN LA DIRECCION 00111 SI EL BIT 0
   %00111 ESTA ACTIVO, EN ESE CASO ES PORQUE
   %EL BUFFER TIENE UN DATO

   C=DAT0 B
   ?CBIT=0 0
   GOYES BUCLE
   $1B41100 %REGISTRO 00114
           %TRAIGO DOS NIBLES 00114 Y 00115
           %SALVO EL STACK
   A=0 W
   A=DAT0 B

ASRB B %SHIFT DERECHA UN BIT
ASRB B %SHIFT DERECHA UN BIT
A=-A B    %HAGO: (64-POSICION) =LINEA
A=A+64 B
% ------ Para hacer que grafique del 220 al 255 solamente hay que sumarle 3--
% -------veces mas 64 y sacarle la division de 4 de arriba-------------------
%A=A+64 B
%A=A+64 B
%A=A+64 B
LC 00022  %carga 34
GOSBVL =MUL#    %(#03991) B[A] = A[A] * C[A]
C=R0 A   %CARGA EL PRINCIPIO DEL BODY DEL GROB
B=B+C A
C=D1 A %CARGA EL VALOR DE LA POSICION DEL ULTIMO NIBBLE GUARDADO
?C>=B A
GOYES DECREMENTO
GOSUBL INCREMENTO
GOTO LINEA
*DECREMENTO
C=C-34 A
?C>B A
GOYES DECREMENTO
*LINEA
%RECOMPONER EL DATO E IMPRIMIRLO
D1=C
LC 11
DAT1=C B   %imprime el dato en pantalla
%desplazamiento lateral
GOSUBL DESPLAZAMIENTO
GOLONG PRINCIPAL

*ON
%-----------------rutina de tecla ON para salir-----------------
ST=0 15
*LOOP1
GOSBVL =CINRTN
?CBIT=0 15
RTNYES
ST=1 15
GOSUB REINT
LOADRPL
*REINT
RTI
*INCREMENTO
?C>=B A
RTNYES
C=C+34 A
GOTO INCREMENTO
%--------------rutina de desplazamiento lateral-------------
*DESPLAZAMIENTO
LA 40  %CARGA UN CONTADOR DE 64 LINEAS
C=R0 A  %TRAE EL PRINCIPIO DEL CUERPO DEL GROB
D0=C
LC 1000000000000000
B=C W
*CUENTA
C=DAT0 W
?C=B S
GOYES ADD
GOTO SIGUIENTE
*ADD
CSL W    %SHIFT UN BIT SIN CARRY
DAT0=C W  %IMPRIME LA PRIMER PARTE DEL DATO
D0=D0+ 16 %AVANZO AL PROXIMO MEDIO SECTOR
C=DAT0 W
CSL W
CBIT=1 0
DAT0=C W   %IMPRIME LA SEGUNDA PARTE DEL DATO
D0=D0+ 18  %PASA A LA PROXIMA LINEA (16+18=34)
GOTO CUENTALINE
*SIGUIENTE
CSL W
DAT0=C W
D0=D0+ 16
C=DAT0 W
CSL W
DAT0=C W
D0=D0+ 18

?A=0 B
RTNYES
*CUENTALINE
A=A-1 B  %BAJA EL CONTADOR
GOTO CUENTA



ENDCODE
DROP
TURNMENUON
 RECLAIMDISP
CLOSEUART


;
